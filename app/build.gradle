apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.1b1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        buildConfigField "String", "ACRA_URL", project.hasProperty('AcraUrl') ? '"' + project['AcraUrl'] + '"' : "\"\"";
        buildConfigField "String", "ACRA_LOGIN", project.hasProperty('AcraLogin') ? '"' + project['AcraLogin'] + '"' : "\"\"";
        buildConfigField "String", "ACRA_PASSWORD", project.hasProperty('AcraPassword') ? '"' + project['AcraPassword'] + '"' : "\"\"";
    }

    signingConfigs {
        release
        betaConfig {
            storeFile file("keystore.beta.jks")
            storePassword "betabeta"
            keyAlias "beta"
            keyPassword "betabeta"
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        beta {
            debuggable false
            jniDebugBuild false
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile "proguard-rules.txt"
            signingConfig signingConfigs.betaConfig
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(group: 'ch.acra', name: 'acra', version: '4.5.0') {
        exclude(group: 'org.json', module: 'json')
    }
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:gridlayout-v7:19.1.0'
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.squareup.picasso:picasso:2.2+'
    compile 'com.android.support:support-annotations:+'

    compile(group: 'com.google.http-client', name: 'google-http-client-android', version: '1.18.0-rc') {
        exclude(group: 'xpp3', module: 'xpp3')
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude(group: 'junit', module: 'junit')
        exclude(group: 'com.google.android', module: 'android')
    }
}

if (hasProperty('StoreFile')) {
    android.signingConfigs.release.storeFile = file(StoreFile)
    android.signingConfigs.release.storePassword = StorePassword
    android.signingConfigs.release.keyAlias = KeyAlias
    android.signingConfigs.release.keyPassword = KeyPassword
} else {
    android.buildTypes.release.signingConfig = null
}


